load("@bazel_binaries//:defs.bzl", "bazel_binaries")
load(
    "@rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_tests",
    "integration_test_utils",
)
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")
load("//bzlformat:defs.bzl", "bzlformat_pkg")
load("//ci:defs.bzl", "ci_integration_test_params")

bzlformat_pkg(name = "bzlformat")

# MARK: - Integration Tests

sh_binary(
    name = "missing_pkgs_test_runner",
    testonly = True,
    srcs = ["missing_pkgs_test.sh"],
    data = [
        "@buildifier_prebuilt//buildozer",
        "@rules_bazel_integration_test//tools:create_scratch_dir",
    ],
    deps = [
        "//shlib/lib:assertions",
        "//shlib/lib:messages",
        "//shlib/lib:paths",
        "@rules_shell//shell/runfiles",
    ],
)

bazel_integration_tests(
    name = "missing_pkgs_test",
    timeout = "eternal",
    bazel_binaries = bazel_binaries,
    bazel_versions = bazel_binaries.versions.all,
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS + [
        # Avoid file permssion error when using disk and repository cache after
        # 7.0.0rc2 upgrade.
        # https://github.com/bazelbuild/bazel/issues/19908
        "no-sandbox",
    ],
    test_runner = ":missing_pkgs_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("workspace") + [
        "//:workspace_integration_test_files",
    ],
    workspace_path = "workspace",
)

test_suite(
    name = "smoke_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        integration_test_utils.bazel_integration_test_name(
            ":missing_pkgs_test",
            bazel_binaries.versions.current,
        ),
    ],
    visibility = ["//:__subpackages__"],
)

test_suite(
    name = "all_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = integration_test_utils.bazel_integration_test_names(
        ":missing_pkgs_test",
        bazel_binaries.versions.all,
    ),
    visibility = ["//:__subpackages__"],
)

ci_integration_test_params(
    name = "missing_pkgs_test_params",
    bzlmod_modes = ["enabled"],
    oss = [
        "macos",
        "linux",
    ],
    test_names = integration_test_utils.bazel_integration_test_names(
        "missing_pkgs_test",
        bazel_binaries.versions.all,
    ),
    visibility = ["//:__subpackages__"],
)

alias(
    name = "all_test_params",
    actual = ":missing_pkgs_test_params",
    visibility = ["//:__subpackages__"],
)
