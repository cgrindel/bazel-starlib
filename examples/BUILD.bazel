load("@bazel_binaries//:defs.bzl", "bazel_binaries")
load(
    "@rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_tests",
    "default_test_runner",
    "integration_test_utils",
)
load("//bzlformat:defs.bzl", "bzlformat_pkg")
load("//bzllib:defs.bzl", "lists")

bzlformat_pkg(name = "bzlformat")

# Do not have Gazelle process the child workspace
# gazelle:exclude bzlmod_e2e/**

# MARK: - Integration Tests

_EXAMPLE_DIRS = ["bzlmod_e2e"]

default_test_runner(name = "default_test_runner")

[
    bazel_integration_tests(
        name = "{}_test".format(dirname),
        timeout = "eternal",
        bazel_binaries = bazel_binaries,
        bazel_versions = bazel_binaries.versions.all,
        target_compatible_with = select({
            "@cgrindel_bazel_starlib//bzlmod:is_enabled": [],
            "//conditions:default": ["@platforms//:incompatible"],
        }),
        test_runner = ":default_test_runner",
        workspace_files = integration_test_utils.glob_workspace_files(dirname) + [
            "//:workspace_integration_test_files",
        ],
        workspace_path = dirname,
    )
    for dirname in _EXAMPLE_DIRS
]

test_suite(
    name = "smoke_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        integration_test_utils.bazel_integration_test_name(
            ":{}_test".format(dirname),
            bazel_binaries.versions.current,
        )
        for dirname in _EXAMPLE_DIRS
    ],
    visibility = ["//:__subpackages__"],
)

test_suite(
    name = "integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = lists.flatten([
        integration_test_utils.bazel_integration_test_names(
            ":{}_test".format(dirname),
            bazel_binaries.versions.all,
        )
        for dirname in _EXAMPLE_DIRS
    ]),
    visibility = ["//:__subpackages__"],
)

# We do not want to pull everything into the release archive. We just need the
# bzlmod_e2e workspace.
filegroup(
    name = "all_files",
    srcs = glob(["bzlmod_e2e/**"]),
    visibility = ["//:__subpackages__"],
)
