load("@bazel_binaries//:defs.bzl", "bazel_binaries")
load(
    "@rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_tests",
    "integration_test_utils",
)
load("//bzlformat:defs.bzl", "bzlformat_pkg")
load("//ci:defs.bzl", "ci_integration_test_params")

bzlformat_pkg(name = "bzlformat")

# Do not have Gazelle process the child workspace
# gazelle:exclude simple/**

filegroup(
    name = "doc_files",
    srcs = glob(["*.md"]) + [
        # The child workspaces do not appear as packages in the parent workspace.
        # Hence, we reference the relevant doc files as files, not a target.
        "simple/README.md",
    ],
    visibility = ["//:markdown_test_visibility"],
)

# MARK: - Integration Tests

sh_binary(
    name = "simple_test_runner",
    testonly = True,
    srcs = ["simple_test.sh"],
    data = [
        "@rules_bazel_integration_test//tools:create_scratch_dir",
        # NOTE: The test runnder does not use buildozer. However, the child
        # workspace does not find it properly if we don't specify it here.
        "@buildifier_prebuilt//buildozer",
    ],
    deps = [
        "//shlib/lib:assertions",
        "//shlib/lib:messages",
        "//shlib/lib:paths",
        "@bazel_tools//tools/bash/runfiles",
    ],
)

bazel_integration_tests(
    name = "simple_test",
    timeout = "eternal",
    bazel_binaries = bazel_binaries,
    bazel_versions = bazel_binaries.versions.all,
    test_runner = ":simple_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("simple") + [
        "//:workspace_integration_test_files",
    ],
    workspace_path = "simple",
)

test_suite(
    name = "smoke_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        integration_test_utils.bazel_integration_test_name(
            ":simple_test",
            bazel_binaries.versions.current,
        ),
    ],
    visibility = ["//:__subpackages__"],
)

test_suite(
    name = "all_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = integration_test_utils.bazel_integration_test_names(
        ":simple_test",
        bazel_binaries.versions.all,
    ),
    visibility = ["//:__subpackages__"],
)

ci_integration_test_params(
    name = "simple_test_params",
    bzlmod_modes = [
        "enabled",
        "disabled",
    ],
    oss = [
        "macos",
        "linux",
    ],
    test_names = integration_test_utils.bazel_integration_test_names(
        "simple_test",
        bazel_binaries.versions.all,
    ),
    visibility = ["//:__subpackages__"],
)

alias(
    name = "all_test_params",
    actual = ":simple_test_params",
    visibility = ["//:__subpackages__"],
)
