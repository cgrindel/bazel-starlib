load(
    "@cgrindel_rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_test",
    "bazel_integration_tests",
    "default_test_runner",
    "integration_test_utils",
)
load("//:bazel_versions.bzl", "CURRENT_BAZEL_VERSION", "SUPPORTED_BAZEL_VERSIONS")
load("//bzlformat:defs.bzl", "bzlformat_pkg")

bzlformat_pkg(name = "bzlformat")

filegroup(
    name = "doc_files",
    srcs = glob(["*.md"]) + [
        # The child workspaces do not appear as packages in the parent workspace.
        # Hence, we reference the relevant doc files as files, not a target.
        "build_buildtools/README.md",
        "simple/README.md",
    ],
    visibility = ["//:markdown_test_visibility"],
)

# MARK: - Integration Tests

sh_binary(
    name = "simple_test_runner",
    testonly = True,
    srcs = ["simple_test.sh"],
    data = [
        "@cgrindel_rules_bazel_integration_test//tools:create_scratch_dir",
        # NOTE: The test runnder does not use buildozer. However, the child
        # workspace does not find it properly if we don't specify it here.
        "@bazel_starlib_buildtools//:buildozer",
    ],
    deps = [
        "//shlib/lib:assertions",
        "//shlib/lib:messages",
        "//shlib/lib:paths",
        "@bazel_tools//tools/bash/runfiles",
    ],
)

bazel_integration_tests(
    name = "simple_test",
    timeout = "eternal",
    bazel_versions = SUPPORTED_BAZEL_VERSIONS,
    test_runner = ":simple_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("simple") + [
        "//:workspace_integration_test_files",
    ],
    workspace_path = "simple",
)

default_test_runner(
    name = "build_buildtools_test_runner",
    bazel_cmds = [
        "info",
        "test //...",
        "run //:bzlformat_missing_pkgs_test",
    ],
)

bazel_integration_test(
    name = "build_buildtools_test",
    timeout = "eternal",
    bazel_version = CURRENT_BAZEL_VERSION,
    test_runner = ":build_buildtools_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("build_buildtools") + [
        "//:workspace_integration_test_files",
    ],
    workspace_path = "build_buildtools",
)

test_suite(
    name = "smoke_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        integration_test_utils.bazel_integration_test_name(
            ":simple_test",
            CURRENT_BAZEL_VERSION,
        ),
        ":build_buildtools_test",
    ],
    visibility = ["//:__subpackages__"],
)

test_suite(
    name = "integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = integration_test_utils.bazel_integration_test_names(
        ":simple_test",
        SUPPORTED_BAZEL_VERSIONS,
    ) + [
        ":build_buildtools_test",
    ],
    visibility = ["//:__subpackages__"],
)
